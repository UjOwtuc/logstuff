// vim: ft=rust :
use std::str::FromStr;

use crate::ast;

grammar;

pub Identifier: ast::Identifier = <r"[a-zA-Z_][a-zA-Z0-9._-]*"> => ast::Identifier::from(<>.to_string());

Integer: i64 = <r"(0|-?[1-9][0-9]*)"> => i64::from_str(<>).unwrap();
Float: f64 = <r"-?(0|[1-9][0-9]*)\.[0-9]+"> => f64::from_str(<>).unwrap();
QuotedString: String = {
    <s:r#""([^\\"]|\\[tnr\\"])*""#> =>
        s[1..s.len() - 1]
            .replace("\\\"", "\"")
            .replace("\\t", "\t")
            .replace("\\n", "\n")
            .replace("\\r", "\r")
            .replace("\\\\", "\\"),
    <s:r#"'([^\\']|\\[tnr\\'])*'"#> =>
        s[1..s.len() - 1]
            .replace("\\'", "'")
            .replace("\\t", "\t")
            .replace("\\n", "\n")
            .replace("\\r", "\r")
            .replace("\\\\", "\\"),
};

Numeric: ast::Scalar = {
    Integer => ast::Scalar::from(<>),
    Float => ast::Scalar::from(<>),
}

pub Scalar: ast::Scalar = {
    Numeric,
    QuotedString => ast::Scalar::from(<>),
}

pub List: Vec<ast::Scalar> = {
    "()" => Vec::new(),
    "(" <mut v:(<Scalar> ",")*> <e:Scalar> ")" => {
        v.push(e);
        v
    }
};

pub Term: Box<ast::Expression> = {
    <id:Identifier> "=" <v:Scalar> => Box::new(ast::Expression::Compare(id, ast::Operator::Eq, ast::Value::from(v))),
    <id:Identifier> "=" <v:List> => Box::new(ast::Expression::Compare(id, ast::Operator::Eq, ast::Value::from(v))),
    <id:Identifier> "<" <v:Numeric> => Box::new(ast::Expression::Compare(id, ast::Operator::Lt, ast::Value::from(v))),
    <id:Identifier> "<=" <v:Numeric> => Box::new(ast::Expression::Compare(id, ast::Operator::Le, ast::Value::from(v))),
    <id:Identifier> ">" <v:Numeric> => Box::new(ast::Expression::Compare(id, ast::Operator::Gt, ast::Value::from(v))),
    <id:Identifier> ">=" <v:Numeric> => Box::new(ast::Expression::Compare(id, ast::Operator::Ge, ast::Value::from(v))),
    <id:Identifier> "like" <v:QuotedString> => Box::new(ast::Expression::Compare(id, ast::Operator::Like, ast::Value::from(v))),
    <id:Identifier> "in" <v:List> => Box::new(ast::Expression::Compare(id, ast::Operator::In, ast::Value::from(v))),
    <QuotedString> => Box::new(ast::Expression::FullTextSearch(<>)),
}

pub Expression: Box<ast::Expression> = {
    <lhs:Expression> "or" <rhs:AndExpr> => Box::new(ast::Expression::Or(lhs, rhs)),
    AndExpr,
}

AndExpr: Box<ast::Expression> = {
    <lhs:AndExpr> "and" <rhs:NegatedExpr> => Box::new(ast::Expression::And(lhs, rhs)),
    NegatedExpr,
}

NegatedExpr: Box<ast::Expression> = {
    "not" <expr:ParenthesizedExpr> => Box::new(ast::Expression::Not(expr)),
    ParenthesizedExpr,
}

ParenthesizedExpr: Box<ast::Expression> = {
    "(" <e:Expression> ")" => e,
    Term,
}
